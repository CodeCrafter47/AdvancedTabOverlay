/*
 * Copyright (C) 2016 Florian Stober
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
    repositories {
        maven {
            url "http://repository.jboss.org/nexus/content/groups/public/"
        }
        mavenLocal()
        mavenCentral()
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

group 'de.codecrafter47.taboverlay'
version '1.3.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        url "http://nexus.codecrafter47.dyndns.eu/content/repositories/public/"
    }
    maven {
        url "https://repo.dmulloy2.net/nexus/repository/public/"
    }
    maven {
        name "CodeMC"
        url "https://repo.codemc.org/repository/maven-public"
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

    compileOnly "com.google.code.findbugs:jsr305:3.0.1"
    compileOnly "org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT"
    compileOnly "io.netty:netty-all:4.1.9.Final"
    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "4.5.0"
    compileOnly "me.clip.placeholderapi:PlaceholderAPI:1.8.5"
    implementation "de.codecrafter47.taboverlay:taboverlaycommon-api:1.0-SNAPSHOT"
    implementation "de.codecrafter47.taboverlay:taboverlaycommon-config:1.0-SNAPSHOT"
    implementation "de.codecrafter47.data:api:1.0.2-SNAPSHOT"
    implementation "de.codecrafter47.data:minecraft:1.0.2-SNAPSHOT"
    implementation "de.codecrafter47.data.bukkit:api:1.0.2-SNAPSHOT"
    implementation "de.codecrafter47.data:bukkit:1.0.2-SNAPSHOT"
    implementation "codecrafter47.util:chat-utils:1.1-SNAPSHOT"
    implementation "org.bstats:bstats-bukkit:1.4"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    repositories {
        maven {
            name "deployment"
            url "http://nexus.codecrafter47.dyndns.eu/content/repositories/snapshots/"
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

processResources {
    filesMatching("plugin.yml") {
        expand(project.properties)
    }
}

shadowJar {
    relocate 'codecrafter47.util', 'de.codecrafter47.taboverlay.libs.util'
    relocate 'org.bstats', 'de.codecrafter47.taboverlay.libs.bstats'
    relocate 'it.unimi.dsi.fastutil', 'de.codecrafter47.taboverlay.libs.fastutil'
    relocate 'org.yaml.snakeyaml', 'de.codecrafter47.taboverlay.libs.snakeyaml'
    classifier = null
    minimize()
}
